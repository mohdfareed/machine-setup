_pwd=$(pwd) # needed due to some plugins changing the current directory

# region - Setup
# =============================================================================

# homebrew
if [[ -f "/opt/homebrew/bin/brew" ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv)" # activate homebrew

    # setup brew python and pyenv
    export PATH="$(brew --prefix python)/libexec/bin:$PATH"
    if [[ -d "$(brew --prefix pyenv)" ]]; then
        eval "$(pyenv init -)"
        eval "$(pyenv virtualenv-init -)"
    fi
fi

# zinit
repo=https://github.com/zdharma-continuum/zinit.git
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone $repo $ZINIT_HOME
source $ZINIT_HOME/zinit.zsh; unset repo
source "$(realpath $(dirname $0))/plugins.zsh"

# history
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/zcompcache"

# endregion
# region - Functions and Aliases
# =============================================================================

alias zsh::reload='exec $SHELL'
alias cat='bat --paging=never'
alias vim='nvim'
alias ls='eza --group-directories-first --sort=Name --icons'
alias lst='ls -T'
alias lls='ls -lhmU --git --no-user'
alias llst='lls -T'

# time the startup of shell
time-startup() {
    usage="usage: $0 [iterations]"
    if (($# > 1)); then echo $usage && return 1; fi
    for i in $(seq 1 ${1-1}); do time $SHELL -i -c exit; done
}

# activate python virtual environment
venv-activate() {
    usage="usage: $0 [venv_dir]"
    if (($# > 1)); then echo $usage && return 1; fi

    venv=${1-.venv}
    if [[ ! -d "$venv" ]]; then
        echo "No virtual environment directory found"
        return 1
    fi

    source "$venv/bin/activate"
}

# uninstall homebrew package with its dependencies
brew-uninstall() {
    usage="usage: $0 package..."
    if (($# < 1)); then echo $usage && return 1; fi

    brew uninstall $@
    brew autoremove
    brew cleanup
}

# endregion

cd $_pwd; unset _pwd
